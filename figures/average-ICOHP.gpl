set terminal eps enhanced color font "Helvetica,10" linewidth 1 size 8cm,13cm
set locale "cs_CZ.UTF-8"
set encoding utf8

set multiplot layout 5,1 margins 0.15,0.99,0.07,0.999 spacing 0.1,0.005

set key top right sample 0.1
set pointsize 0.8
set format x ''
set mxtics 2
set mytics 2

set label 1 at graph 0.035,0.88 "(a)"

set ytics 0.05
set xrange [-0.02:1.02]
set yrange [-6.27:-6.03]
plot "average-ICOHP.txt" u 1:2 pt 5 lc rgb 'red' t "B-B NN"

set label 1 at graph 0.035,0.88 "(b)"

unset yrange
set key bottom right
set ytics 0.1
set yrange [-2.58:-2.25]
plot "average-ICOHP.txt" u 1:4 pt 7 lc rgb 'blue' t "B-1st Mo NN", \
     "average-ICOHP.txt" u 1:6 pt 9 lc rgb 'gold' t "B-1st Nb NN", \
     "average-ICOHP.txt" u 1:8 pt 5 lc rgb 'red' t "B-1st metal NN"

set label 1 at graph 0.035,0.88 "(c)"

set ylabel "Average ICOHP [eV]" 
set yrange [-1.65:-0.75]
set ytics 0.2
plot "average-ICOHP.txt" u 1:10 pt 7 lc rgb 'blue' t "B-2nd Mo NN", \
     "average-ICOHP.txt" u 1:12 pt 9 lc rgb 'gold' t "B-2nd Nb NN", \
     "average-ICOHP.txt" u 1:14 pt 5 lc rgb 'red' t "B-2nd metal NN"

set label 1 at graph 0.035,0.88 "(d)"

unset ylabel
set ytics 0.1
unset yrange
set key bottom right
set yrange [-4.25:-3.75]
plot "average-ICOHP.txt" u 1:16 pt 7 lc rgb 'blue' t "C-Mo NN", \
     "average-ICOHP.txt" u 1:18 pt 9 lc rgb 'gold' t "C-Nb NN", \
     "average-ICOHP.txt" u 1:20 pt 5 lc rgb 'red' t "C-metal NN"

set label 1 at graph 0.035,0.58 "(e)"

set xlabel "{/Helvetica-Italic x} in (Mo_{1-{/Helvetica-Italic x}}Nb_{/Helvetica-Italic x})_2BC"
set format x "%.1f"
set ytics 0.2
set yrange [-1.9:-0.75]
plot "average-ICOHP.txt" u 1:22 pt 7 lc rgb 'blue' t "Mo-Mo NN", \
     "average-ICOHP.txt" u 1:24 pt 9 lc rgb 'gold' t "Nb-Nb NN", \
     "average-ICOHP.txt" u 1:26 pt 11 lc rgb 'green' t "Mo-Nb NN", \
     "average-ICOHP.txt" u 1:28 pt 5 lc rgb 'red' t "metal-metal NN"
